apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'com.tease.androidactionstest'
def LIB_ARTIFACT_ID = 'androidlibraryactionstest'
def LIB_VERSION = '1.1.7'

task sourceJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier "sources"
}

publishing {
	repositories {
		maven {
			name = "GithubPackages"
			url = uri("https://maven.pkg.github.com/vlas-ilya/AndroidLibraryActionTest")
			credentials {
				username = System.getenv('GITHUB_USER') ?: project.properties['GITHUB_USER']
				password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: project.properties['GITHUB_PERSONAL_ACCESS_TOKEN']
			}
		}
		maven {
			name = 'CustomMavenRepo'
			url = "file://${buildDir}/repo"
		}
	}
	publications {
		AndroidLibraryActionsTest(MavenPublication) {
			groupId LIB_GROUP_ID
			artifactId LIB_ARTIFACT_ID
			version LIB_VERSION
			artifact(sourceJar)
			artifact("file://${buildDir}/outputs/aar/AndroidLibraryActionTest.aar")
		}
	}
}

task cleanBuildPublish {
	dependsOn 'clean'
	dependsOn 'build'
	dependsOn 'publish'
	tasks.findByName('build').mustRunAfter 'clean'
	tasks.findByName('publish').mustRunAfter 'build'
}